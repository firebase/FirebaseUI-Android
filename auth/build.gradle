apply plugin: 'com.android.library'
apply from: '../library/quality/quality.gradle'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion targetSdk
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            manifestPlaceholders = [enableFbLogging: true]

            minifyEnabled false
            consumerProguardFiles getDefaultProguardFile('proguard-android.txt'), 'auth-proguard.pro'
        }

        debug {
            // This is needed for tests to pass. Facebook automatically enables app logging
            // which causes a null pointer exception while running tests.
            manifestPlaceholders = [enableFbLogging: false]
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                exceptionFormat = 'full'
            }
        }
    }
}

dependencies {
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"

    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.android.gms:play-services-auth:$firebaseVersion"

    provided 'com.facebook.android:facebook-android-sdk:4.23.0'
    provided("com.twitter.sdk.android:twitter-core:3.0.0@aar") { transitive = true }

    // The following libraries are needed to prevent incompatibilities with the facebook
    // library when updating com.android.support libraries:
    compile "com.android.support:cardview-v7:$supportLibraryVersion"

    testCompile 'junit:junit:4.12'
    //noinspection GradleDynamicVersion
    testCompile 'org.mockito:mockito-core:2.8.+'
    testCompile 'org.robolectric:robolectric:3.2.2'
    // See https://github.com/robolectric/robolectric/issues/1932#issuecomment-219796474
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testCompile 'com.facebook.android:facebook-android-sdk:4.23.0'
}

javadoc.exclude([
        'com/firebase/ui/auth/provider/**',
        'com/firebase/ui/auth/ui/ExtraConstants',
        'com/firebase/ui/auth/ui/email/field_validators/**',
        'com/firebase/ui/auth/util/**',
])

javadoc.include([
        'com/google/firebase/auth/**'
])
