apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
check.dependsOn 'checkstyle', 'findbugs', 'pmd'

def configDir = "${project.rootDir}/library/quality"
def reportsDir = "${project.buildDir}/reports"

checkstyle {
    toolVersion = '8.3'
}

pmd {
    toolVersion = '5.8.1'
}

task checkstyle(type: Checkstyle) {
    configFile file("$configDir/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files()
}

task findbugs(type: FindBugs, dependsOn: "assembleDebug") {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("$configDir/findbugs-filter.xml")
    classes = files("${project.rootDir}/app/build/intermediates/classes",
            "${project.rootDir}/auth/build/intermediates/classes",
            "${project.rootDir}/storage/build/intermediates/classes",
            "${project.rootDir}/database/build/intermediates/classes")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$reportsDir/findbugs.xml"
        }
        html {
            destination "$reportsDir/findbugs.html"
        }
    }

    classpath = files()
}

task pmd(type: Pmd) {
    ignoreFailures = true
    ruleSetFiles = files("$configDir/pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = false
        html.enabled = true
        xml {
            destination "$reportsDir/pmd.xml"
        }
        html {
            destination "$reportsDir/pmd.html"
        }
    }
}

android {
    lintOptions {
        disable 'WrongConstant', // TODO it's being unruly, figure that out later
                'ObsoleteLintCustomCheck', // TODO ButterKnife will fix this in v9.0
                'IconExpectedSize',
                'NewerVersionAvailable', 'GradleDependency' // For reproducible builds

        baseline file("$configDir/lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        htmlOutput file("$reportsDir/lint-results.html")
        xmlOutput file("$reportsDir/lint-results.xml")
    }
}
