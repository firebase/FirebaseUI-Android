apply plugin: 'com.android.application'
apply from: '../library/quality/quality.gradle'

android {
    compileSdkVersion compileSdk

    defaultConfig {
        applicationId "com.firebase.uidemo"
        minSdkVersion 16
        targetSdkVersion targetSdk
        versionCode 1
        versionName version

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            // For the purposes of the sample, allow testing of a proguarded release build
            // using the debug key
            signingConfig signingConfigs.debug

            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation "com.google.firebase:firebase-core:$firebaseVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation 'com.android.support:multidex:1.0.3'

    implementation project(path: ':auth')
    implementation project(path: ':firestore')
    implementation project(path: ':database')
    implementation project(path: ':storage')

    implementation 'com.facebook.android:facebook-login:4.32.0'
    // Needed to override Facebook
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation("com.twitter.sdk.android:twitter-core:3.1.1@aar") { transitive = true }

    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    // Used for FirestorePagingActivity
    implementation "android.arch.paging:runtime:$pagingVersion"

    // The following dependencies are not required to use the Firebase UI library.
    // They are used to make some aspects of the demo app implementation simpler for
    // demonstrative purposes, and you may find them useful in your own apps; YMMV.
    implementation "android.arch.lifecycle:runtime:$architectureVersion"
    implementation 'pub.devrel:easypermissions:1.2.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

apply plugin: 'com.google.gms.google-services'
