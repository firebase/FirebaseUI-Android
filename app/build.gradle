apply plugin: 'com.android.application'
apply from: '../library/quality/quality.gradle'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "com.firebase.uidemo"
        minSdkVersion 16
        targetSdkVersion targetSdk
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')

            // For the purposes of the sample, allow testing of a proguarded release build
            // using the debug key
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:design:$supportLibraryVersion"

    compile project(path: ':auth')
    compile project(path: ':database')
    compile project(path: ':storage')

    compile "com.google.android.gms:play-services-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"
    compile "com.google.firebase:firebase-storage:$firebaseVersion"

    compile('com.facebook.android:facebook-android-sdk:4.23.0')
    compile("com.twitter.sdk.android:twitter-core:3.0.0@aar") { transitive = true }

    // The following dependencies are not required to use the Firebase UI library.
    // They are used to make some aspects of the demo app implementation simpler for
    // demonstrative purposes, and you may find them useful in your own apps; YMMV.
    compile 'pub.devrel:easypermissions:0.4.2'
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}

apply plugin: 'com.google.gms.google-services'
